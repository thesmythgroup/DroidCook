// https://docs.gradle.org/current/userguide/userguide.html
// http://google.github.io/android-gradle-dsl/current/

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}

ext.srcCompat = 1.8

ext.acl = {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility rootProject.ext.srcCompat
        targetCompatibility rootProject.ext.srcCompat
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    buildTypes { release { minifyEnabled false } }

    lintOptions { abortOnError false }
}

ext.bintray = {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["$project.name"]
    pkg {
        repo = "maven"
        name = "droidcook"
        licenses = ["BSD 2-clause"]
        vcsUrl = "https://github.com/thesmythgroup/DroidCook.git"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    group = "com.codesmyth.droidcook"
    version = "0.7.0"
}
